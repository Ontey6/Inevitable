import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
   id("java")
   id("xyz.jpenilla.run-paper") version "2.3.1"
   id("io.papermc.paperweight.userdev") version "2.0.0-beta.19"
   id("com.gradleup.shadow") version "9.3.0"
}

group = "com.ontey"
version = "1.0"

repositories {
   mavenCentral()
   maven {
      name = "papermc-repo"
      url = "https://repo.papermc.io/repository/maven-public/"
   }
   maven {
      url = "https://repo.extendedclip.com/releases/"
   }
}

dependencies {
   // paper
   compileOnly("io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT")
   
   // NMS (paperweight userdev)
   paperweight.paperDevBundle("1.21.1-R0.1-SNAPSHOT")
   
   // PlaceholderAPI
   //TODO sometimes throws and stops server (prob gradle config problem)
   compileOnly("me.clip:placeholderapi:2.11.7")
   
   // lombok
   compileOnly("org.projectlombok:lombok:1.18.42")
   annotationProcessor("org.projectlombok:lombok:1.18.42")
   testCompileOnly("org.projectlombok:lombok:1.18.42")
   testAnnotationProcessor("org.projectlombok:lombok:1.18.42")
}

tasks {
   runServer {
      // Configure the Minecraft version for our task.
      // This is the only required configuration besides applying the plugin.
      // Your plugin's jar (or shadowJar if present) will be used automatically.
      minecraftVersion("1.21")
   }
   
   // my personal script that copies the jar to my test server
   if(file("local.gradle").exists())
      apply from: "local.gradle"
}

def targetJavaVersion = 21
java {
   def javaVersion = JavaVersion.toVersion(targetJavaVersion)
   sourceCompatibility = javaVersion
   targetCompatibility = javaVersion
   if (JavaVersion.current() < javaVersion) {
      toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
   }
}

tasks.withType(JavaCompile).configureEach {
   options.encoding = 'UTF-8'
   options.release.set(targetJavaVersion)
}

processResources {
   def props = [version: version]
   inputs.properties props
   filteringCharset = 'UTF-8'
   filesMatching('plugin.yml') {
      expand(props)
   }
}

tasks.named('shadowJar', ShadowJar) {
   enableAutoRelocation = true
   relocationPrefix = "inevitable.relocated"
}